// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    // Put your code here:
    Inc16(in=currstate, out=plusone);
    Mux16(a=currstate, b=plusone, sel=inc, out=pluscurr);
    Mux16(a=pluscurr, b=in, sel=load, out=incplus);
    Mux16(a=incplus, b=false,sel=reset, out=increset);
    Or(a=reset, b=load, out=flagrl);
    Or(a=flagrl, b=inc, out=flagload);
    Register(in=increset, load=flagload, out=currstate, out=out);
}
